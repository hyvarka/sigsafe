# vim: ft=python
# $Id$
# Copyright (C) 2004 Scott Lamb <slamb@slamb.org>.
# This file is part of sigsafe, which is released under the MIT license.

SConscript('platform_behavior/SConscript')
SConscript('race_checker/SConscript')

Import('env')
env = env.Copy()
env.Prepend(LIBS = ['sigsafe', 'm'])

for i in ['simple_test',
          'test_pipe_bytecount',
          'test_sock_bytecount']:
    env.Program(target = i, source = i + '.c')

for i in [ #flags        #postfix
          ([],           'raw'),
          (['DO_SAFE'],  'safe'),
          (['DO_SETJMP'],'setjmp'),
          (['DO_SELECT'],'select')]:
    obj = env.StaticObject(target = 'bench_read_' + i[1] + '.o',
                           source = 'bench_read.c',
                           CPPDEFINES = i[0])
    env.Program(target = 'bench_read_' + i[1], source = obj)
